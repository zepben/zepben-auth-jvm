plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'idea'
    id 'maven-publish'
}

group 'com.zepben'
version '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    maven {
        url "https://mavenrepo.zepben.com/repository/maven-releases/"
        credentials {
            username nexusUsername
            password nexusPassword
        }
    }
    maven {
        url "https://mavenrepo.zepben.com/repository/maven-snapshots/"
        credentials {
            username nexusUsername
            password nexusPassword
        }
    }

    mavenCentral()
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

ext {
    grpc_version = "1.28.1" // CURRENT_GRPC_VERSION
    vertx_version = "3.9.0"
    kotlin_version = '1.3.72'
    junit_jupiter_version = '5.6.2'
    hamcrest_version = '2.2'
}
dependencies {
    implementation 'com.auth0:java-jwt:3.10.3'
    implementation 'com.auth0:jwks-rsa:0.11.0'
    implementation "com.zepben:vertx-route-utils:5.0.0"
    implementation "io.vertx:vertx-core:$project.ext.vertx_version"
    implementation "io.vertx:vertx-web:$project.ext.vertx_version"
    implementation "io.vertx:vertx-lang-kotlin:$project.ext.vertx_version"
    implementation "io.vertx:vertx-lang-kotlin-coroutines:$project.ext.vertx_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "io.grpc:grpc-netty-shaded:$project.ext.grpc_version"
    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation 'io.vertx:vertx-auth-common:3.9.0'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$project.ext.kotlin_version"
    testImplementation "org.junit.jupiter:junit-jupiter:$project.ext.junit_jupiter_version"
    testImplementation "org.hamcrest:hamcrest:$project.ext.hamcrest_version"
    testImplementation "com.zepben.test:test-utils:2.0-SNAPSHOT"
    testImplementation "com.zepben:vertx-rest-testing-utils:2.2.0"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        languageVersion = 1.3
        freeCompilerArgs = ["-XXLanguage:+NewInference"]
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        languageVersion = 1.3
        freeCompilerArgs = ["-XXLanguage:+NewInference"]
    }
}

publishing {
    publications {
        ncLibrary(MavenPublication) {
            from components.java
        }
    }

    repositories {
        if (rootProject.hasProperty("nexusUsername") && rootProject.hasProperty("nexusPassword")) {
            maven {
                credentials {
                    username nexusUsername
                    password nexusPassword
                }
                def nexusReleasesRepo = "https://mavenrepo.zepben.com/repository/maven-releases/"
                def nexusSnapshotsRepo = "https://mavenrepo.zepben.com/repository/maven-snapshots/"
                url = version.endsWith('SNAPSHOT') ? nexusSnapshotsRepo : nexusReleasesRepo
            }
        }
    }
}
